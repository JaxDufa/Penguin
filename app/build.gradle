plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id "com.google.protobuf" version "0.8.12"
}

android {
    compileSdk 32

    Properties properties = new Properties()
    properties.load(project.rootProject.file("local.properties").newDataInputStream())

    defaultConfig {
        applicationId "com.my.penguin"
        minSdk 23
        targetSdk 32
        versionCode 1
        versionName "1.0"

        buildConfigField "String", "API_URL", "\"https://openexchangerates.org/api/\""
        buildConfigField "String", "API_KEY", properties.getProperty("api.id", "")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.core:core-splashscreen:1.0.0-beta01'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1'
    implementation 'androidx.datastore:datastore:1.0.0'

    // https://github.com/protocolbuffers/protobuf/releases
    implementation "com.google.protobuf:protobuf-javalite:3.19.4"

    // https://github.com/square/moshi/blob/master/CHANGELOG.md
    implementation 'com.squareup.moshi:moshi:1.13.0'
    kapt 'com.squareup.moshi:moshi-kotlin-codegen:1.13.0'

    // https://github.com/square/retrofit/blob/master/CHANGELOG.md
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.3'

    // https://github.com/InsertKoinIO/koin/blob/main/CHANGELOG.md
    implementation 'io.insert-koin:koin-core:3.1.5'
    implementation 'io.insert-koin:koin-android:3.1.5'

    // https://github.com/Kotlin/kotlinx.coroutines/releases
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.0'

    // Tests
    testImplementation 'junit:junit:4.13.2'

    // https://github.com/mockk/mockk/releases
    testImplementation 'io.mockk:mockk:1.12.0'

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    testImplementation 'io.insert-koin:koin-test:3.1.5'
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.0'
    testImplementation 'org.robolectric:robolectric:4.7.3'

    debugImplementation 'androidx.fragment:fragment-testing:1.4.1'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.11.0"
    }

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}